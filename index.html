<html>
  <head>
    <title>Messy WIP Three.JS port of bwhmather's remake of the missile game 3d.</title>
    <style>
      body { margin: 0; }
      canvas { width: 100%; height: 100% }
      * {cursor: none}
    </style>
  </head>
  <body>
    <script id="vertexShader" type="x-shader/x-vertex">
      varying vec2 euclid;
      varying float pixSizeFinal;
      varying float fade;
      uniform float pixSize;
      void main() {
        euclid = vec2(position.x, position.y);
        vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
        gl_Position = projectionMatrix * mvPosition;
        pixSizeFinal = pixSize * gl_Position.z;
        fade = smoothstep(0.01, 0.2, gl_Position.z);
      }
    </script>
    <script id="barrierShaderBase" type="x-shader/x-fragment">
varying highp vec2 euclid;
varying highp float pixSizeFinal;
varying highp float fade;
uniform highp float fogDensity;
uniform highp float grey;

float signedDistance( const vec2 coord);

float sdfLine(vec2 a, vec2 b, vec2 coord) {
  vec2 ld = b - a;
  vec2 pd = vec2(ld.y, -ld.x);
  vec2 dp = a - coord;
  return dot(normalize(pd), dp);
}

float sdfCircle( const vec2 c, const float r, const vec2 coord ) {
  vec2 offset = coord - c;

  return sqrt((offset.x * offset.x) + (offset.y * offset.y)) - r;
}

float sdfUnion( const float a, const float b ) {
  return min(a, b);
}

float sdfDifference( const float a, const float b) {
  return max(a, -b);
}

float sdfIntersection( const float a, const float b ) {
  return max(a, b);
}

vec2 render( const float distance, const float pixSizeFinal, const float strokeWidth, const float gamma, const float grey) {
  float halfStroke = strokeWidth * 0.5;
  float halfGamma = gamma * 0.5;

  if (distance < -halfStroke + pixSizeFinal) {
    float factor = smoothstep(-halfStroke - pixSizeFinal, -halfStroke + pixSizeFinal, distance);
    return vec2(mix(grey, 0.0, factor * halfGamma), 1.0);
  } else if (distance <= halfStroke - pixSizeFinal) {
    return vec2(0.0, 1.0);
  } else {
    float factor = smoothstep(halfStroke - pixSizeFinal, halfStroke + pixSizeFinal, distance);
    return vec2(0.0, 1.0 - factor * halfGamma);
  }
}

void main() {
  float d = signedDistance(euclid);
  vec2 sample = render(d, pixSizeFinal, 0.010, float(GAMMA_FACTOR), grey);
  float zw = gl_FragCoord.z / gl_FragCoord.w;
  gl_FragColor.xyz = vec3(
    mix(sample.x, 1.0,
        clamp(
          (1.0 - exp2(
            ((-((fogDensity * fogDensity)) * (zw * zw))
             * 1.442695))),
          0.0, 1.0)));
  gl_FragColor.w = sample.y * fade;
}
    </script>
    <script id="barrierBlankShader" type="x-shader/x-fragment">
float signedDistance( const vec2 coord ) {
  float a = sdfCircle(vec2(0.0), 0.495 - pixSizeFinal, coord);
  float b = sdfCircle(vec2(0.0), 0.45, coord);
  float final = sdfDifference(a, b);
  return final;
}
    </script>
    <script id="barrier1Shader" type="x-shader/x-fragment">
float signedDistance( const vec2 coord ) {
  float a = sdfCircle(vec2(0.0), 0.5, coord);
  float b = sdfCircle(vec2(0.0), 0.45, coord);
  float ring = sdfDifference(a, b);
  float upperRight = sdfIntersection(-coord.y, -coord.x);
  float lowerLeft  = sdfIntersection( coord.y,  coord.x);
  float quadrants = sdfUnion(lowerLeft, upperRight);
  float circle = sdfCircle(vec2(0.0), 0.15, coord);
  float addcircle = sdfUnion(quadrants, circle);
  float addring = sdfUnion(addcircle, ring);
  float final = sdfIntersection(a, addring);
  return final;
}
    </script>
    <script id="barrier2Shader" type="x-shader/x-fragment">
float signedDistance( const vec2 coord ) {
  float a = sdfCircle(vec2(0.0), 0.5, coord);
  float c1 = sdfCircle(vec2(0.250, 0.0), 0.15, coord);
  float c2 = sdfCircle(vec2(-0.125, 0.2165), 0.15, coord);
  float c3 = sdfCircle(vec2(-0.125, -0.2165), 0.15, coord);
  float circles12 = sdfUnion(c1, c2);
  float circles = sdfUnion(circles12, c3);
  float final = sdfDifference(a, circles);
  return final;
}
    </script>
    <script id="barrier3Shader" type="x-shader/x-fragment">
float signedDistance( const vec2 coord ) {
  float a = sdfCircle(vec2(0.0), 0.5, coord);
  float b = sdfCircle(vec2(0.0), 0.45, coord);
  float ring = sdfDifference(a, b);
  float lowerright = sdfUnion(coord.y, -coord.x);
  float nixhole = sdfDifference(lowerright, sdfCircle(vec2(0.0), 0.15, coord));
  float addring = sdfUnion(nixhole, ring);
  float final = sdfIntersection(a, addring);
  return final;
}
    </script>
    <script id="barrier4Shader" type="x-shader/x-fragment">
float signedDistance( const vec2 coord ) {
  float a = sdfCircle(vec2(0.0), 0.5, coord);
  float b = sdfCircle(vec2(0.0), 0.45, coord);
  float slopeline = sdfLine(vec2(0.0), vec2(0.154, 0.423), coord);
  float quadrants = sdfIntersection(slopeline, coord.y);
  float hole = sdfIntersection(quadrants, b);
  float cut = sdfDifference(a, hole);
  float circle = sdfCircle(vec2(0.0), 0.15, coord);
  float final = sdfUnion(circle, cut);
  return final;
}
    </script>
    <script id="barrier5Shader" type="x-shader/x-fragment">
float signedDistance( const vec2 coord ) {
    vec2 absc = abs(coord);
    float a = sdfCircle(vec2(0.0), 0.5, absc);
    float b = sdfCircle(vec2(0.0), 0.45, absc);
    float ring = sdfDifference(a, b);
    float line = absc.x - 0.225;
    float circle = sdfCircle(vec2(0.40796, 0.0), 0.29, absc);
    float takecircle = sdfIntersection(-circle, line);
    float addring = sdfUnion(takecircle, ring);
    float final = sdfIntersection(a, addring);
    return final;
}
    </script>
    <script id="barrier6Shader" type="x-shader/x-fragment">
float signedDistance( const vec2 coord ) {
    vec2 absc = abs(coord);
    float a = sdfCircle(vec2(0.0), 0.5, absc);
    float linex = absc.x - 0.4;
    float liney = absc.y - 0.125;
    float rect = sdfIntersection(linex, liney);
    float final = sdfDifference(a, rect);
    return final;
}
    </script>
    <script id="barrierFinishShader" type="x-shader/x-fragment">
float signedDistance( const vec2 coord ) {
    float a = sdfCircle(vec2(0.0), 0.5, coord);
    float b = sdfCircle(vec2(0.0), 0.45, coord);
    float c = sdfCircle(vec2(0.0), 0.375, coord);
    float ring = sdfDifference(a, b);
    float line = sdfLine(vec2(-0.318, 0.318), vec2(0.318, -0.318), coord);
    float uniona = sdfUnion(line, coord.x);
	float unionb = sdfUnion(-line, -coord.x);
    float bottomright = sdfDifference(-uniona, -coord.y);
    float topleft = sdfDifference(-unionb, coord.y);
    float both = sdfUnion(bottomright, topleft);
    float takecenter = sdfDifference(both, c);
    float addring = sdfUnion(ring, takecenter);
    float final = sdfIntersection(addring, a);
    return final;
}
    </script>
    <script src="bower_components/threejs/build/three.min.js"></script>
    <script src="app.js"></script>
  </body>
</html>
